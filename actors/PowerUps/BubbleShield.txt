ACTOR BubbleShield : CustomInventory Replaces BlurSphere
{
  +VISIBILITYPULSE
  +INVENTORY.UNDROPPABLE
  +INVENTORY.PERSISTENTPOWER
  +INVENTORY.INVBAR
  Inventory.MaxAmount 1
  Inventory.Icon "BUBDB0"
  Inventory.PickupSound "Powerup/ShieldMsg"
  RenderStyle Translucent
  Inventory.PickupMessage "You Got the DXB-2750 Protector Reusable Bubble Shield"
  States
  {
  Spawn:
    PINS ABCDCB 2 Bright
    loop
  Use:
    TNT1 A 0 A_JumpIfInventory("UsingShield", 1, "UseMessage")
    TNT1 A 0 A_JumpIfInventory("ShieldDepleted", 1, "BubRechargeMessage")
    TNT1 A 0 A_GiveInventory("PowerBubble")
    TNT1 A 0 A_Overlay(3, "UseShield")
    Fail
  UseShield:
    TNT1 A 0 A_GiveInventory("UsingShield", 1)
    TNT1 A 0 A_OverlayFlags(3, PSPF_ALPHA, 1)
    TNT1 A 0 A_OverlayAlpha(3, 0.5)
    TNT1 A 0 A_PlaySound("Powerup/ShieldUp",CHAN_AUTO)
    BUBS ABCDEFGH 3
    TNT1 A 0 A_PlaySound("Powerup/ShieldMsg",CHAN_AUTO)
    TNT1 A 0 A_Print("Shield activation successful. Sustaining for 30 seconds.")
    goto ShieldSustain
  ShieldSustain:
    BUBS I 980
    TNT1 A 0 A_PlaySound("Powerup/ShieldWarn",CHAN_AUTO)
    TNT1 A 0 A_Print("Warning, Shield break imminent")
    BUBS I 70
    Goto ShieldCrash
  ShieldCrash:
    TNT1 A 0 A_PlaySound("Powerup/ShieldBrk",CHAN_AUTO)
    BUBS IHGF 4
    BUBD A 4
    BUBD B 4
    BUBD C 4
    BUBD D 4
    TNT1 A 0 A_PlaySound("Powerup/ShieldMsg",CHAN_AUTO)
    TNT1 A 0 A_Print("Shield depleted, initiating recharge cycle.")
    TNT1 A 0 A_TakeInventory("UsingShield")
    TNT1 A 0 A_GiveInventory("ShieldDepleted", 1)
    TNT1 A 2100
    TNT1 A 0 A_TakeInventory("ShieldDepleted", 1)
    TNT1 A 0 A_PlaySound("Powerup/ShieldMsg",CHAN_AUTO)
    TNT1 A 0 A_Print("DBX-2750 is now ready for redeployment")
    TNT1 A 0 A_ClearOverlays(3,3)
    Fail
  BubRechargeMessage:
    TNT1 A 0 A_PlaySound("Powerup/ShieldMsg",CHAN_AUTO)
    TNT1 A 0 A_Print("Please Wait: DBX-2750 Shield is currently recharging, Unable to initiate deployment sequence.")
    Fail
  UseMessage:
    TNT1 A 0 A_PlaySound("Powerup/ShieldMsg",CHAN_AUTO)
    TNT1 A 0 A_Print("Please Wait: DBX-2750 Shield is currently in use")
    Fail
  }
}



Actor ShieldCounter : Inventory
{
    Inventory.MaxAmount 12
}

Actor UsingShield : Inventory
{
    Inventory.MaxAmount 1
}

Actor ShieldDepleted : Inventory
{
    Inventory.MaxAmount 10
}

ACTOR PowerBFLBubble : PowerInvulnerable
{
  Powerup.Duration 350
  Inventory.Icon "BUBDB0"
}

ACTOR PowerBubble : PowerInvulnerable
{
  Powerup.Duration -30
  Inventory.Icon "BUBDB0"
}

